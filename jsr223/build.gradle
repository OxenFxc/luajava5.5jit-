plugins {
    id 'java'
    id 'java-library'
    id 'jacoco'
}

repositories {
    mavenCentral()
}

group = rootProject.group
version = rootProject.version

ext {
    jUnitVersion = '5.10.2'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    api project(':luajava')
    compileOnly project(':luajit-lua5.5')
    testImplementation project(':luajit-lua5.5')
    testImplementation project(path: ':luajit-lua5.5', configuration: 'desktopNatives')
    testImplementation "org.junit.jupiter:junit-jupiter-api:$jUnitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$jUnitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$jUnitVersion"
}

tasks.withType(JavaCompile).configureEach {
    options.deprecation = true
}

tasks.named('jar') {
    manifest {
        attributes('Automatic-Module-Name': 'party.iroiro.jsr223lua')
    }
}

test {
    useJUnitPlatform()

    jacoco {
        destinationFile = layout.buildDirectory.file('jacoco/test.exec').get().asFile
    }
}

jacocoTestReport {
    dependsOn fileTree(layout.buildDirectory).include('jacoco/*.exec')

    executionData.setFrom(fileTree(layout.buildDirectory).include('jacoco/*.exec'))

    reports {
        xml.required = true
        html.required = true
        csv.required = true
        xml.outputLocation = layout.buildDirectory.file('reports/jacoco/report.xml')
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: '**/party/iroiro/luajava/util/**')
        }))
    }
}
